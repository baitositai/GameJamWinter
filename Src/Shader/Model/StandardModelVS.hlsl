// VS/PS共通
#include "../Common/VertexToPixelHeader.hlsli"

// IN
#include "../Common/Vertex/VertexInputType.hlsli"
#define VERTEX_INPUT DX_MV1_VERTEX_TYPE_NMAP_1FRAME

// OUT
#define VS_OUTPUT VertexToPixelLit
#include "../Common/Vertex/VertexShader3DHeader.hlsli"

cbuffer cbParam : register(b7)
{
    float3 g_camera_pos;
    float g_fog_start;
    
    float3 g_light_pos;   
    float g_fog_end;
}

VS_OUTPUT main(VS_INPUT VSInput)
{
    VS_OUTPUT ret;
    
    // 頂点座標変換 +++++++++++++++++++++++++++++++++++++( 開始 )
    float4 lLocalPosition;
    float4 lWorldPosition;
    float4 lViewPosition;
    
    // float3 → float4
    lLocalPosition.xyz = VSInput.pos;
    lLocalPosition.w = 1.0f;
    
    // ローカル座標をワールド座標に変換(剛体)
    lWorldPosition.w = 1.0f;
    lWorldPosition.xyz = mul(lLocalPosition, g_base.localWorldMatrix);
    ret.world = lWorldPosition.xyz;
    
    // ワールド座標をビュー座標に変換
    lViewPosition.w = 1.0f;
    lViewPosition.xyz = mul(lWorldPosition, g_base.viewMatrix);
    ret.vwPos.xyz = lViewPosition.xyz;
    
    // ビュー座標を射影座標に変換
    ret.svPos = mul(lViewPosition, g_base.projectionMatrix);
    
    // 頂点座標変換 +++++++++++++++++++++++++++++++++++++( 終了 )
    // その他、ピクセルシェーダへ引継&初期化 ++++++++++++( 開始 )
    // UV座標
    ret.uv.x = VSInput.uv0.x;
    ret.uv.y = VSInput.uv0.y;
    // 法線
    ret.normal = normalize(mul(VSInput.norm, (float3x3) g_base.localWorldMatrix));
    // ディフューズカラー
    ret.diffuse = VSInput.diffuse;
    // タンジェント
    ret.tan = normalize(mul(VSInput.tan, (float3x3) g_base.localWorldMatrix));
    // バイノーマル
    ret.bin = normalize(mul(VSInput.bin, (float3x3) g_base.localWorldMatrix));
    
    // フォグの強さ
    float foglength = length(lWorldPosition.xyz - g_camera_pos);
    float fog = (g_fog_end - foglength) / (g_fog_end - g_fog_start);
    ret.fogFactor = saturate(fog);
    
    // ポイントライト
    // 1. ライトベクトルの計算
    float lightDit = 200.0f;
    float lightLength = length(lWorldPosition.xyz - g_light_pos);
    ret.lightPower = saturate((lightDit - lightLength) / lightDit);
    
    // その他、ピクセルシェーダへ引継&初期化 ++++++++++++( 終了 )
    // 出力パラメータを返す
    return ret;
}